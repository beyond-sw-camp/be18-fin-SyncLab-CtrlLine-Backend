plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.flywaydb.flyway' version "11.15.0"
    id 'org.sonarqube' version "7.0.1.6134"
}

sonar {
    properties {
        property "sonar.projectKey", "CtrlLine"
        property "sonar.projectName", "CtrlLine"
    }
}

group = 'com.beyond.synclab'
version = '0.0.1-SNAPSHOT'
description = 'CtrlLine'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    // Web & Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'

    // Validation (유효성 검사)
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.7'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.7'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.7'

    // Data
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Devtools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // OpenApi Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'

    // AWS SDK
    implementation 'software.amazon.awssdk:s3:2.26.26'

    // QueryDsl
    // Open Feign QueryDSL (latest security patched version)
    implementation 'io.github.openfeign.querydsl:querydsl-core:7.1'
    implementation 'io.github.openfeign.querydsl:querydsl-apt:7.1'
    implementation 'io.github.openfeign.querydsl:querydsl-jpa:7.1'

    // QueryDSL annotation processor - 별도 configuration으로 분리
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:7.1:jpa"
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    testAnnotationProcessor "io.github.openfeign.querydsl:querydsl-apt:7.1:jpa"
    testAnnotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()

    jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}", '-Xshare:off'
}

// Querydsl 설정
def querydslSrcDir = 'src/main/generated'

// querydsl QClass 파일 생성 위치 지정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslSrcDir))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [querydslSrcDir]
    test.java.srcDirs += [querydslSrcDir]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(querydslSrcDir)
}
